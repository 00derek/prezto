#
# A simple theme from PeepCode.
# http://peepcode.com/blog/2012/my-command-line-prompt
#
# Authors:
#   Geoffrey Grosenbach <boss@topfunky.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/LhgmW.png
#

function prompt_peepcode_git_info_precmd {
  if (( $+functions[git-info] )); then
    git-info
  fi

  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
}

function prompt_peepcode_git_info_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_peepcode_git_info_precmd

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' status 'partial'
  zstyle ':prezto:module:git:info:action' format ' +%s'
  zstyle ':prezto:module:git:info:branch' format ' %F{8}%b%f'
  zstyle ':prezto:module:git:info:commit' format ' %F{white}%.7c%f'
  zstyle ':prezto:module:git:info:unindexed' format ' %F{8}✗%f'
  zstyle ':prezto:module:git:info:keys' format 'rprompt' '%b%c%s%I'

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format ' %F{white}%v%f'

  # Define prompts.
  PROMPT="
%~
%(?.%F{green}${1:-☻ }%f.%F{red}${1:-☻ }%f) "
RPROMPT='${ruby_info[version]}${git_info[rprompt]}'

}

function prompt_peepcode_git_info_help {
  cat <<EOH
This prompt's last command exit status symbol is customizable:

  prompt peepcode [<symbol>]

If this option is not provided, the symbol defaults to ☻.
EOH
}

function prompt_peepcode_git_info_preview {
  local +h PROMPT='%# '
  local +h RPROMPT=''
  local +h SPROMPT=''

  if (( $# > 0 )); then
    prompt_preview_theme 'peepcode' "$@"
  else
    prompt_preview_theme 'peepcode'
    print
    prompt_preview_theme 'peepcode' "❯"
    print
    prompt_preview_theme 'peepcode' "$"
  fi
}

prompt_peepcode_git_info_setup "$@"

